#!/usr/bin/perl
use strict;
use warnings;
use FindBin qw($Bin);
use lib "$Bin/../../PerlLib";
use BRCM::GenConfig;

# $p will allow us to GET values from the PROFILE
# $c will allow us to SET (and GET and DRIVER_SETUP) on the config
# file as we transform it from a template to the final config

# arguments
# * profile file
# * config  file
my $p    = new BRCM::GenConfig(shift);
my $chip = $p->get('BRCM_CHIP');
my $c    = new BRCM::GenConfig( shift, Chip => $chip, Profile => $p );


# list of netfilter modules, minus the "CONFIG_" prefix
my @nf_mods = qw[NETFILTER_XTABLES IP_NF_IPTABLES IP_NF_FILTER
  NETFILTER_XT_TARGET_TCPMSS NETFILTER_XT_TARGET_SKIPLOG
  NETFILTER_NETLINK NETFILTER_NETLINK_QUEUE
  NETFILTER_XT_MATCH_MULTIPORT];

# must match static modules defined in rut_iptables.c (modules[] = ...):
# also note: tcpudp is controled by NETFILTER_XTABLES
my @static_nf_mods = qw[NETFILTER_XTABLES];

if ( $p->get("BUILD_CONNTRACK_TOOLS") ) {

    # add one more to the list
    push @nf_mods, "NF_CT_NETLINK";
    $c->set( "CONFIG_NF_CONNTRACK_EVENTS", 'y' );
}

# for each type of netfilter, the list of modules it needs
# to conditionally enable
my $nf_x_mods = {
    NAT => [
        qw[NF_CONNTRACK NF_CONNTRACK_IPV4
          NF_NAT IP_NF_TARGET_MASQUERADE IP_NF_TARGET_REDIRECT]
    ],

    FIREWALL => [qw[NETFILTER_XT_MATCH_CONNTRACK NETFILTER_XT_MATCH_STATE NETFILTER_XT_MATCH_LIMIT NETFILTER_XT_MATCH_DOS NETFILTER_XT_MATCH_PKTTYPE NETFILTER_XT_MATCH_CONNMARK IP_NF_TARGET_LOG NETFILTER_XT_TARGET_LOG NETFILTER_XT_MATCH_TIME]],

    MANGLE => [
        qw[IP_NF_MANGLE NETFILTER_XT_MATCH_MARK
          NETFILTER_XT_TARGET_MARK NETFILTER_XT_MATCH_DSCP
          NETFILTER_XT_TARGET_DSCP NETFILTER_XT_MATCH_MAC
          NETFILTER_XT_MATCH_LENGTH NETFILTER_XT_MATCH_RECENT
          NETFILTER_XT_MATCH_IPRANGE]
    ],

    DYNDSCP => [qw[NF_CONNTRACK NF_CONNTRACK_IPV4 NF_DYNDSCP]],

    PPPREMOTE => [qw[NF_CONNTRACK NF_CONNTRACK_IPV4 NF_NAT NETFILTER_XT_MATCH_LIMIT]],

    LOG => [qw[IP_NF_TARGET_LOG NETFILTER_XT_TARGET_LOG]],

    LAYER7_FILTER => [qw[NF_CONNTRACK NF_CONNTRACK_IPV4 NETFILTER_XT_MATCH_LAYER7 NETFILTER_XT_TARGET_DC]],
};


# If NAT is enabled, the list of ALGs needed for each module
# Example:
#    FTP  => [qw[NF_CONNTRACK_FTP NF_NAT_FTP]],
# means...
# CONFIG_CF_CONNTRACK and CONFIG_NF_NAT_FTP are both configured
# the same way as the  BRCM_KERNEL_NF_NAT_ALG_FTP profile variable

my $algs = {
    FTP  => [qw[NF_CONNTRACK_FTP NF_NAT_FTP]],
    SIP  => [qw[NF_CONNTRACK_SIP NF_NAT_SIP]],
    TFTP => [qw[NF_CONNTRACK_TFTP NF_NAT_TFTP]],
    H323 => [qw[NF_CONNTRACK_H323 NF_NAT_H323]],

    # H323_SIGNAL_PROXY_ALG_MODULES="H323_SIGNAL_PROXY"
    IRC => [qw[NF_CONNTRACK_IRC NF_NAT_IRC]],

    PPTP => [qw[NF_CONNTRACK_PPTP NF_NAT_PPTP]],
    RTSP => [qw[NF_CONNTRACK_RTSP NF_NAT_RTSP]],
    SNMP => [qw[NF_NAT_SNMP_BASIC]],

    # TALK_ALG_MODULES="TALK NAT_TALK"
};

my $bcm_algs = {
    IPSEC => [qw[NF_CONNTRACK_IPSEC NF_NAT_IPSEC NF_CT_PROTO_ESP NF_NAT_PROTO_ESP]],

    # DTX8_ALG_MODULES="DTX8"
    # WM_ALG_MODULES="WM"
    PT => [qw[NF_NAT_PT]],
};


# prepend "CONFIG_" to each member of @nf_mods
@nf_mods = map( "CONFIG_$_", @nf_mods );
@static_nf_mods = map( "CONFIG_$_", @static_nf_mods );

if ( my $m = $p->get("BRCM_KERNEL_NETFILTER") ) {
    $c->set( "CONFIG_NETFILTER", 'y' );

    # set all of the modules in @nf_mods to either 'y' or 'm'
    # [we pass a reference to the list]
    $c->set( \@nf_mods, $m );
    $c->set( \@static_nf_mods, 'y' );

    # for each of the keys in the nf_x_mods hash
    # set ALL of the values' CONFIG_whatever to the same
    # 'y' or 'm' as BRCM_KERNEL_NF_thekeyname, but don't unset it
    # if is set and BRCM_KERNEL_NF_thekeyname is not
    foreach my $t ( keys( %{$nf_x_mods} ) ) {

        # The map statement makes a copy of the list reference with CONFIG_
        # prepended to each member
        my $r = [ map( "CONFIG_$_", @{ $nf_x_mods->{$t} } ) ];
        $c->set( $r, $p->get("BRCM_KERNEL_NF_$t"), NoUnset => 1 );
    }
    if ( $p->get("BRCM_KERNEL_NF_NAT") ) {

        # for each of the ALGs in the $algs hash (reference),
        # set ALL of the values' CONFIG_whatever to the same
        # state as the ALG itself (but don't unset)
        foreach my $t ( keys( %{$algs} ) ) {
            my $r = [ map( "CONFIG_$_", @{ $algs->{$t} } ) ];
            $c->set( $r, $p->get("BRCM_KERNEL_NF_NAT_ALG_$t"), NoUnset => 1 );
        }
        foreach my $t ( keys( %{$bcm_algs} ) ) {

            # Same thing, but don't let them be static
            my $r = [ map( "CONFIG_$_", @{ $bcm_algs->{$t} } ) ];
            $c->set( $r, $p->get("BRCM_KERNEL_NF_NAT_ALG_$t") ? 'm' : undef, NoUnset => 1 );
        }
    }
    if ( $p->get("BUILD_DYNAHELPER") ) {
        $c->set( "IP_NF_QUEUE",               'm' );
        $c->set( "IP_NF_RAW",                 'm' );
        $c->set( "NETFILTER_BRCM_DYNAHELPER", 'y' );
    }
    if ( $p->get("BRCM_KERNEL_NF_LAYER7_FILTER") || $p->get("BUILD_BRIDGE_NETFILTER") ) {
        $c->set( "CONFIG_BRIDGE_NETFILTER", 'y' );
    }
}
$c->write();

